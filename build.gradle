buildscript {
    ext.kotlin_version = kotlin_version
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath("com.cinnober.gradle:semver-git:2.3.1")
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish' // for local publishing in dev, not for pushing to Nexus

group group_id
version "$base_version$version_suffix"

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ["src/main/kotlin"]
    test.kotlin.srcDirs = test.java.srcDirs = ["src/test/kotlin"]
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name 'nexus'
            def isSnapshot = version.endsWith('SNAPSHOT')
            url "$nexus_repository_root${isSnapshot ? 'snapshots' : 'releases'}/"
            credentials {
                username = System.getenv('NEXUS_USERNAME')
                password = System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()

    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    testLogging {
        events "PASSED", "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
    }
}


compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}


dependencies {
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.12.5"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.12.5"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:4.6.2" // for kotest framework
    testImplementation "io.kotest:kotest-assertions-core-jvm:4.6.2" // for kotest core jvm assertions
    testImplementation "com.h2database:h2:1.4.200"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2"
    implementation "joda-time:joda-time:2.10.10"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.exposed:exposed-core:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jdbc:$exposed_version"
    implementation "org.jetbrains.exposed:exposed-jodatime:$exposed_version"
    implementation "io.github.microutils:kotlin-logging:2.0.11"
    implementation "org.postgresql:postgresql:42.2.23"
}


